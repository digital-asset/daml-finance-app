-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.App.Distribution.Subscription.Service where

import DA.Action (foldlA)
import DA.Optional (whenSome)
import DA.Set (fromList, singleton)
import Daml.Finance.App.BackToBack.Service qualified as BackToBack
import Daml.Finance.App.Distribution.Subscription.Model (BackToBack, Offering(..), Status(..), Subscription)
import Daml.Finance.App.Distribution.Subscription.Util (settleSubscription)
import Daml.Finance.App.Settlement.Service qualified as Settlement (Service)
import Daml.Finance.Interface.Util.Disclosure qualified as Disclosure (AddObservers(..), I)
import Daml.Finance.Interface.Account.Account qualified as Account (R)
import Daml.Finance.Interface.Holding.Fungible qualified as Fungible (I)
import Daml.Finance.Interface.Account.Util (getAccount)
import Daml.Finance.Interface.Types.Common.Types (AccountKey, InstrumentQuantity, Parties)
import Daml.Finance.Interface.Util.Lockable qualified as Lockable (Acquire(..), I, LockType(..), Release(..))

template Service
  with
    operator : Party
    provider : Party
    customer : Party
    settlementServiceCid : ContractId Settlement.Service
  where
    signatory operator, provider, customer

    key (operator, provider, customer) : (Party, Party, Party)
    maintainer key._1

    nonconsuming choice CreateOffering : ContractId Offering
      with
        offeringId : Text
        asset : InstrumentQuantity
        price : InstrumentQuantity
        customerHoldingCid : ContractId Fungible.I
        customerAccount : AccountKey
        backToBack : Optional BackToBack
        observers : Parties
      controller customer
      do
        -- Disclose holding
        customerHolding <- fetch customerHoldingCid
        customerHoldingCid : ContractId Fungible.I <- coerceContractId <$> exercise (coerceContractId customerHoldingCid : ContractId Disclosure.I) Disclosure.AddObservers with disclosers = singleton customer; observersToAdd = (offeringId, singleton provider)
        -- Disclose delivering securities account
        let customerSendingAccount = getAccount customerHolding
        (_, delRef) <- fetchByKey @Account.R customerSendingAccount
        exercise (coerceContractId delRef.cid : ContractId Disclosure.I) Disclosure.AddObservers with disclosers = singleton customer; observersToAdd = (offeringId, singleton provider)
        -- Disclose receiving cash account
        (_, recRef) <- fetchByKey @Account.R customerAccount
        exercise (coerceContractId recRef.cid : ContractId Disclosure.I) Disclosure.AddObservers with disclosers = singleton customer; observersToAdd = (offeringId, singleton provider)
        -- Lock holding
        customerHoldingCid <- coerceContractId <$> exercise (toInterfaceContractId customerHoldingCid : ContractId Lockable.I) Lockable.Acquire with newLockers = fromList [ provider ]; context = offeringId; lockType = Lockable.Semaphore

        -- Lock back-to-back assets
        lockedB2b <-
          case backToBack of
            Some b2b -> do Some <$> exerciseByKey @BackToBack.Service (operator, b2b.party, customer) BackToBack.LockOffering with backToBack = b2b
            None -> pure None

        create Offering
          with
            operator; provider; asset; price; offeringId
            issuer = customer
            issuerHoldingCid = customerHoldingCid
            issuerAccount = customerAccount
            backToBack = lockedB2b
            status = Open
            observers

    nonconsuming choice ProcessOffering : ContractId Offering
      with
        offeringCid : ContractId Offering
        subscriptionCids : [ContractId Subscription]
      controller provider
      do
        -- Settle subscriptions
        offering <- fetch offeringCid
        subscriptions <- zip subscriptionCids <$> forA subscriptionCids fetch
        (remainingIssuerQuantity, remainingIssuerHoldingCidOpt) <- foldlA (settleSubscription provider settlementServiceCid offering) (offering.asset.amount, Some offering.issuerHoldingCid) subscriptions

        -- Settle back-to-back trade
        whenSome offering.backToBack \b2b -> do
          let
            totalQuantity = sum $ map ((.quantity) . snd) subscriptions
          exerciseByKey @BackToBack.Service (operator, b2b.party, customer) BackToBack.SettleOffering with offeringCid; totalQuantity
          pure ()

        -- Archive offer and subscriptions
        archive offeringCid
        -- forA_ subscriptionCids archive

        -- Unlock remaining issuer holding
        whenSome remainingIssuerHoldingCidOpt \holdingCid -> do
          exercise (toInterfaceContractId holdingCid : ContractId Lockable.I) Lockable.Release with context = offering.offeringId
          pure ()

        create offering with status = Closed

    choice Terminate : ()
      with
        actor : Party
      controller actor
      do
        assert $ actor == operator || actor == provider || actor == customer
        pure ()

template Offer
  with
    operator : Party
    provider : Party
    customer : Party
    settlementServiceCid : ContractId Settlement.Service
  where
    signatory operator, provider
    observer customer

    choice Accept : ContractId Service
      controller customer
      do
        create Service with ..

    choice Decline : ()
      controller customer
      do
        return ()

    choice Withdraw : ()
      controller provider
      do pure ()

template Request
  with
    provider : Party
    customer : Party
  where
    signatory customer
    observer provider

    choice Cancel : ()
      controller customer
      do pure ()

    choice Reject : ()
      controller provider
      do pure ()

    choice Approve : ContractId Service
      with
        operator : Party
        settlementServiceCid : ContractId Settlement.Service
      controller operator, provider
      do
        create Service with ..
