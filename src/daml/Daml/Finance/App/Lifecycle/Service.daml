-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.App.Lifecycle.Service where

import DA.Set (singleton)
import ContingentClaims.Claim (Claim)
import ContingentClaims.Lifecycle qualified as Lifecycle
import ContingentClaims.Math.Stochastic (Expr)
import ContingentClaims.Observation (Observation(..))
import ContingentClaims.Util (expiry, fixings, payoffs)
import Daml.Finance.Interface.Data.Observable qualified as Observable (I)
import Daml.Finance.Interface.Instrument.Base.Instrument qualified as Instrument (I, K, Q, R)
import Daml.Finance.Interface.Instrument.Equity.Instrument qualified as Equity (I, DeclareDividend(..), DeclareReplacement(..), DeclareStockSplit(..))
import Daml.Finance.Interface.Claims.Claim qualified as Claim (Claim, getClaims)
import Daml.Finance.Interface.Claims.Types (C, Pending, TaggedClaim)
import Daml.Finance.Interface.Claims.Util.Lifecycle qualified as Claim (lifecycle, lifecycleClaims, timeEvent)
import Daml.Finance.Interface.Lifecycle.Effect qualified as Effect (I)
import Daml.Finance.Interface.Lifecycle.Clock qualified as Clock (I)
import Daml.Finance.Interface.Lifecycle.Event qualified as Event (I)
import Daml.Finance.Interface.Lifecycle.Rule.Lifecycle qualified as Lifecycle (I, Evolve(..))
import Daml.Finance.Interface.Util.Disclosure qualified as Disclosure (I)
import Daml.Finance.Interface.Types.Common (Id, InstrumentKey(..))
import Daml.Finance.App.Lifecycle.Pricing qualified as Pricing
import Daml.Finance.App.Structuring.Auto.Service qualified as Structuring

template Service
  with
    operator : Party
    provider : Party
    customer : Party
    distributionRuleCid : ContractId Lifecycle.I
    replacementRuleCid : ContractId Lifecycle.I
  where
    signatory operator, provider, customer

    key (operator, provider, customer) : (Party, Party, Party)
    maintainer key._1

    nonconsuming choice DeclareDividend : [ContractId Effect.I]
      with
        clockCid : ContractId Clock.I
        equity : Instrument.K
        newVersion : Text
        id : Id
        description : Text
        effectiveDate : Date
        perUnitDistribution : [ Instrument.Q ]
      controller customer
      do
        now <- getTime
        (_, equityRef) <- fetchByKey @Instrument.R equity
        equity <- fetch equityRef.cid
        let
          disclosure = toInterface @Disclosure.I equity
          v = view equity
          dv = view disclosure
          newInstrument = InstrumentKey with depository = provider; issuer = customer; id = v.id; version = newVersion
        exerciseByKey @Structuring.Service (operator, provider, customer) Structuring.RequestAndCreateEquity with id = v.id; description = v.description; version = newVersion; validAsOf = now; observers = dv.observers
        eventCid <- exercise (coerceContractId equityRef.cid : ContractId Equity.I) Equity.DeclareDividend with id; description; effectiveDate; newInstrument; perUnitDistribution
        snd <$> exercise distributionRuleCid Lifecycle.Evolve with ruleName = "Dividend"; settlers = singleton provider; eventCid; clockCid; observableCids = []

    nonconsuming choice DeclareReplacement : [ContractId Effect.I]
      with
        clockCid : ContractId Clock.I
        equity : Instrument.K
        id : Id
        description : Text
        effectiveDate : Date
        perUnitReplacement : [ Instrument.Q ]
      controller customer
      do
        (_, equityRef) <- fetchByKey @Instrument.R equity
        eventCid <- exercise (coerceContractId equityRef.cid : ContractId Equity.I) Equity.DeclareReplacement with id; description; effectiveDate; perUnitReplacement
        snd <$> exercise replacementRuleCid Lifecycle.Evolve with ruleName = "StockSplit"; settlers = singleton provider; eventCid; clockCid; observableCids = []

    nonconsuming choice DeclareStockSplit : [ContractId Effect.I]
      with
        clockCid : ContractId Clock.I
        equity : Instrument.K
        newVersion : Text
        id : Id
        description : Text
        effectiveDate : Date
        adjustmentFactor : Decimal
      controller customer
      do
        now <- getTime
        (_, equityRef) <- fetchByKey @Instrument.R equity
        equity <- fetch equityRef.cid
        let
          disclosure = toInterface @Disclosure.I equity
          v = view equity
          dv = view disclosure
          newInstrument = InstrumentKey with depository = provider; issuer = customer; id = v.id; version = newVersion
        exerciseByKey @Structuring.Service (operator, provider, customer) Structuring.RequestAndCreateEquity with id = v.id; description = v.description; version = newVersion; validAsOf = now; observers = dv.observers
        eventCid <- exercise (coerceContractId equityRef.cid : ContractId Equity.I) Equity.DeclareStockSplit with id; description; effectiveDate; newInstrument; adjustmentFactor
        snd <$> exercise replacementRuleCid Lifecycle.Evolve with ruleName = "StockSplit"; settlers = singleton provider; eventCid; clockCid; observableCids = []

    nonconsuming choice PreviewLifecycle : ([TaggedClaim], [Pending])
      with
        today : Time
        observableCids : [ContractId Observable.I]
        instrumentCid : ContractId Claim.Claim
      controller customer
      do
        instrument <- fetch instrumentCid
        claims <- exercise self GetCurrentClaims with instrumentCid; observableCids
        Claim.lifecycleClaims observableCids ((view instrument).acquisitionTime) claims [Claim.timeEvent today]

    nonconsuming choice SimulateLifecycle : [[Lifecycle.Pending Time Instrument.K]]
      with
        today : Time
        prices : [Decimal]
        instrumentCid : ContractId Claim.Claim
      controller customer
      do
        instrument <- fetch instrumentCid
        claims <- mconcat . map (.claim) <$> Claim.getClaims instrument
        let
          simulate price = do
            result <- Lifecycle.lifecycle (\_ t -> pure price) claims today today
            pure result.pending
        mapA simulate prices

    nonconsuming choice GetCurrentClaims : [TaggedClaim]
      with
        instrumentCid : ContractId Claim.Claim
        observableCids : [ContractId Observable.I]
      controller customer
      do
        hasClaims <- fetch instrumentCid
        instrument <- fetch (coerceContractId instrumentCid : ContractId Instrument.I)
        fst <$> Claim.lifecycle observableCids hasClaims [Claim.timeEvent (view instrument).validAsOf]

    nonconsuming choice Lifecycle : (ContractId Lifecycle.I, [ContractId Effect.I])
      with
        ruleName : Text
        eventCid : ContractId Event.I
        clockCid : ContractId Clock.I
        observableCids : [ContractId Observable.I]
        lifecyclableCid : ContractId Lifecycle.I
      controller customer
      do
        exercise lifecyclableCid Lifecycle.Evolve with ruleName; settlers = singleton provider; eventCid; clockCid; observableCids

    nonconsuming choice Expiry : Optional Time
      with
        claims : C
      controller customer
      do
        pure $ expiry claims

    nonconsuming choice Underlying : [Instrument.K]
      with
        claims : C
      controller customer
        do
          pure $ snd <$> payoffs claims

    nonconsuming choice Payoffs : [(Observation Time Decimal Text, Instrument.K)]
      with
        claims : C
      controller customer
        do
          pure $ payoffs claims

    nonconsuming choice Fixings : [Time]
      with
        claims : C
      controller customer
        do
          pure $ fixings claims

    nonconsuming choice Multipliers : [Decimal]
      with
        claims : C
      controller customer
        do
          let getConst : (Observation Time Decimal a, b) -> [Decimal]
              getConst (Const x, _) = [x]
              getConst _            = []
          return $ concatMap getConst $ payoffs claims

    nonconsuming choice PreviewPricing : (Expr Text, Text)
      with
        ccy : Text
        claims : Claim Text Decimal Text Text
      controller customer
      do
        let formula = Pricing.valueClaim ccy claims
        pure (formula, Pricing.print formula)

    choice Terminate : ()
      with
        actor : Party
      controller actor
      do
        assert $ actor == operator || actor == provider || actor == customer
        pure ()

template Offer
  with
    operator : Party
    provider : Party
    customer : Party
    distributionRuleCid : ContractId Lifecycle.I
    replacementRuleCid : ContractId Lifecycle.I
  where
    signatory operator, provider
    observer customer

    choice Accept : ContractId Service
      controller customer
      do
        create Service with ..

    choice Decline : ()
      controller customer
      do pure ()

    choice Withdraw : ()
      controller provider
      do pure ()

template Request
  with
    customer : Party
    provider : Party
  where
    signatory customer
    observer provider

    choice Cancel : ()
      controller customer
      do pure ()

    choice Reject : ()
      controller provider
      do pure ()

    choice Approve : ContractId Service
      with
        operator : Party
        distributionRuleCid : ContractId Lifecycle.I
        replacementRuleCid : ContractId Lifecycle.I
      controller operator, provider
      do
        create Service with ..
